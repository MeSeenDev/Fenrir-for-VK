apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 28
        versionCode 19
        versionName '1.19'

        buildConfigField 'int', 'VK_API_APP_ID', getProps("vk_app_id")
        buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("vk_client_secret")
        buildConfigField 'String', 'SERVICE_TOKEN', getProps("vk_service_token")
        buildConfigField 'String', 'FAKE_ABI', getProps("fake_abi")
        buildConfigField 'String', 'FAKE_DEVICE', getProps("fake_device")

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=compatibility']
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
        }
    }

    flavorDimensions "type"

    productFlavors {
        market_full {
            targetSdkVersion 29
            applicationId "dev.ragnarok.fenrir_full"
            dimension "type"
            buildConfigField 'int', 'IS_DONATE', '1'
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("youtube_dev_key")
            buildConfigField 'String', 'FCM_SESSION_ID_GEN_URL', getProps("fcm_session_id_gen_url")
        }
        market_lite {
            targetSdkVersion 29
            applicationId "dev.ragnarok.fenrir_public"
            dimension "type"
            buildConfigField 'int', 'IS_DONATE', '0'
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("youtube_dev_key")
            buildConfigField 'String', 'FCM_SESSION_ID_GEN_URL', getProps("fcm_session_id_gen_url")
        }
        for_telegram {
            targetSdkVersion 28
            applicationId "dev.ragnarok.fenrir_open"
            dimension "type"
            buildConfigField 'int', 'IS_DONATE', '2'
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("telega_youtube_dev_key")
            buildConfigField 'String', 'FCM_SESSION_ID_GEN_URL', getProps("telega_fcm_session_id_gen_url")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('app.build_config')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if (fis != null) fis.close()
        }
    } else {
        throw new GradleException('File app.build_config not found')
    }
}

ext {
    exoLibraryVersion = '2.12.2'
}

dependencies {
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0-alpha05'
    implementation 'androidx.annotation:annotation:1.2.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-alpha01'
    implementation 'com.google.android.material:material:1.3.0-alpha04'
    implementation 'com.squareup.picasso:picasso:2.8'
    implementation 'io.coil-kt:coil:1.1.0'
    implementation 'io.coil-kt:coil-gif:1.1.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0-alpha02'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.customview:customview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.webkit:webkit:1.4.0-rc02'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.exifinterface:exifinterface:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.8'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.google.firebase:firebase-database:19.5.1'
    implementation('com.google.firebase:firebase-messaging:21.0.0') {
        exclude group: "com.google.firebase", module: "firebase-installations"
    }
    implementation project(path: ':firebase-installations')
    implementation project(path: ':ealvatag')
    implementation project(path: ':crashx')
    implementation project(path: ':slidr')
    implementation project(path: ':ucrop')
    implementation project(path: ':zxing-android-embedded')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoLibraryVersion"
    implementation "com.google.android.exoplayer:exoplayer-hls:$exoLibraryVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.biometric:biometric:1.2.0-alpha01'
    implementation 'com.github.TutorialsAndroid:FilePicker:v8.0.19'
    implementation 'androidx.media:media:1.2.1'
    implementation 'androidx.activity:activity-ktx:1.1.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.5'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.5.0-beta02'
    implementation 'com.google.guava:guava:30.0-android'
    implementation 'com.caverock:androidsvg:1.4'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
}

apply plugin: 'com.google.gms.google-services'
